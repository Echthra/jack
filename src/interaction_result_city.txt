# interaction_result_city
!Шаблоны обработки 
sp_txt = 1
 
!Тип взаимодействия - разговор
IF $interaction_type = 'standart':
 dynamic $special_part   
END

!Тип взаимодействия - приглашение от покупателя
IF $interaction_type = 'byer_invitation':
 gs '$slave_price'
 npc = 1
 dynamic '$text[1] = $<<$npc_id>>_pref[1]'
 IF npc_progress[$npc_id] = 2: 
  $text[1] = '   [<<$npc_name[$npc_id]>>]
   - Мне пока не нужны новые рабы. Но я замолвлю словечко друзьям, уверен что на такой товар покупатели найдутся без труда.'
  else
  IF slave_state = CONST_INT['slave_exist'] and slave['brand'] > 0:
   txt = 1
   $yes = {$interaction_type = 'sell_slave_simple'
   gt 'interaction_result_city'}
   $no = {gt 'city_menu_form'}
   gt 'choice_screen'
  END
 END
END

!Тип взаимодействия - продажа рабыни по упрощенной схеме
IF $interaction_type = 'sell_slave_simple':
 txt_count = 1
 if slave_specialisation[$interesting_specialisation[$npc_id]] => 1 and rating => demand_rating[$npc_id] and slave[$npc_id] = 0: byer_interested = 1 
 n = 13 + 2 * demand_rating[$npc_id]
 demand = rand(0,n)
 if demand > charm and byer_interested = 1: byer_interested += no_charm_check
 IF byer_interested = 1:
  npc_progress[$npc_id] = 2
  $text[1] = $buy_quote[$npc_id]
  sparks += common_price[demand_rating[$npc_id]]
  slave_state = CONST_INT['no_slave']
  IF fixed_brand_rise = 0:
   if brand_rate < demand_rating[$npc_id]: brand_rate += demand_rating[$npc_id]
   !if faction_fame[$npc_alignment[$npc_id]] < demand_rating[$npc_id]: dynamic $rise_faction_fame
   IF faction_fame[$npc_alignment[$npc_id]] < demand_rating[$npc_id]: 
    no_rise = 0
    if $npc_alignment[$npc_id] = 'taurus' and faction_fame['kamira'] > 2: no_rise = 1
    if $npc_alignment[$npc_id] = 'serpis' and faction_fame['korvus'] > 2: no_rise = 1
    if $npc_alignment[$npc_id] = 'kamira' and faction_fame['taurus'] > 2: no_rise = 1
    if $npc_alignment[$npc_id] = 'korvus' and faction_fame['serpis'] > 2: no_rise = 1
    if noble_status > 0 and faction_fame[$npc_alignment[$npc_id]] => 4: no_rise = 1
    if no_rise = 0: faction_fame[$npc_alignment[$npc_id]] += 1
   END
   else
   brand_rate += fixed_brand_rise
  END
  else
  if demand > charm: $text[1] = $refuse_charm_quote[$npc_id] 
  if slave_specialisation[$interesting_specialisation[$npc_id]] < 1: $text[1] = $refuse_skill_quote[$npc_id]  
  if demand_rating[$npc_id] > rating: $text[1] = $refuse_level_quote[$npc_id]  
  if slave[$npc_id] = 1: $text[1] = $refuse_again_quote[$npc_id]
  if demand > charm: slave[$npc_id] = 1
 END
END

!Тип взаимодействия - продажа рабыни по расширенной схеме схеме
IF $interaction_type = 'sell_slave_complex':
 txt_count = 1
 gs 'slave_sell_chek'
 IF byer_interested = 1:
  $text[1] = $buy_quote[$npc_id]
  if  noprice = 0: sparks += common_price[demand_rating[$npc_id]]
  slave_state = CONST_INT['no_slave']
  if brand_rate < demand_rating[$npc_id]: brand_rate += demand_rating[$npc_id]
  !faction_fame[$npc_alignment[$npc_id]] < demand_rating[$npc_id]: dynamic $rise_faction_fame
   IF faction_fame[$npc_alignment[$npc_id]] < demand_rating[$npc_id]: 
    no_rise = 0
    if $npc_alignment[$npc_id] = 'taurus' and faction_fame['kamira'] > 2: no_rise = 1
    if $npc_alignment[$npc_id] = 'serpis' and faction_fame['korvus'] > 2: no_rise = 1
    if $npc_alignment[$npc_id] = 'kamira' and faction_fame['taurus'] > 2: no_rise = 1
    if $npc_alignment[$npc_id] = 'korvus' and faction_fame['serpis'] > 2: no_rise = 1
    if noble_status > 0 and faction_fame[$npc_alignment[$npc_id]] => 4: no_rise = 1
    if no_rise = 0: faction_fame[$npc_alignment[$npc_id]] += 1
   END  
  npc_progress[$npc_id] = 2
  active[$quest[5]] = 0
  if noble_status = 0 and demand_rating[$npc_id] = 5: noble_status = 1
  else
  if sl_txt = 103: $text[1] = $refuse_charm_quote[$npc_id]  
  if sl_txt = 102: $text[1] = $refuse_skill_quote[$npc_id]  
  if sl_txt = 101: $text[1] = $refuse_level_quote[$npc_id]  
  if sl_txt = 100: $text[1] = $refuse_special_quote[$npc_id]
 END
END

!Тип взаимодействия - брина обучает героя суперудару
IF $interaction_type = 'learn_spm':
 $place = 'colloseum'
 $return_lock  = 'next_day'  
 npc = 1
 $npc_id = 'брина_рыжая' 
 txt_count = 2 
 $text[1] = '   [РЫЖАЯ БРИНА]
   - Так! Сильнее!! Резче!!! Ты дерешься хуже двенадцатилетней девочки, работорговец. Хх-а, получай! Чувствуешь? Вот так это должно быть. Это почувствует твой враг! Вставай, рохля! Ещё раз. Сильнее!! Резче!!!'
 $text[2] = '   Брина гоняет вас по тренировочной арене целый день, и в итоге вы чувствуете себя выжатым как лимон. От демонстраций ударов у вас синяки по всему телу. А ведь она еще и искры за это берет! Ну, по крайней мере, теперь вы знаете новый мощный приём. Теперь домой. Отдыхать...' 
 if $master_spm[1] = '': $master_spm[1] = $spm_to_learn & jump 'spm_loop'
 if $master_spm[2] = '': $master_spm[2] = $spm_to_learn & jump 'spm_loop'
 if $master_spm[3] = '': $master_spm[3] = $spm_to_learn & jump 'spm_loop'
 if $master_spm[4] = '': $master_spm[4] = $spm_to_learn & jump 'spm_loop'
 if $master_spm[5] = '': $master_spm[5] = $spm_to_learn & jump 'spm_loop'  
 :spm_loop
 $spm_learned[$spm_to_learn] = 1
 spm_number += 1
END

!Тип взаимодействия - продажа рабыни
IF $interaction_type = 'slave_sold':
 personal_relations[$npc_id] += client_statisfaction
 IF client_statisfaction > 0 and npc_bennies[$npc_id] > 0:
  bennies_give = 1 + rating - demand_rating[$npc_id]
  IF bennies_give <=  npc_bennies[$npc_id]:
   brand_rate += bennies_give
   npc_bennies[$npc_id] -= bennies_give
   else
   brand_rate += npc_bennies[$npc_id]
   npc_bennies[$npc_id] = 0
  END
 END
 IF client_statisfaction < 0 and npc_bennies[$npc_id] > 0:
  brand_rate += client_statisfaction
  npc_bennies[$npc_id] += client_statisfaction
  if npc_bennies[$npc_id] < 0: npc_bennies[$npc_id] = 0
 END
 $return_lock = $return_lock_transfer 
 slave_state = CONST_INT['no_slave']
 killvar 'slave'
 killvar '$slave' 
 killvar 'slave_rate'
 $slave_name = '' 
 sparks += sell_price
 rr = RAND (0,10)
 no_slave_count[$npc_id] = count_size[$npc_id] + rr
 txt_count = 1
 $place = $place_transfer   
END

!Тип взаимодействия - демонстрация апартаментов для аренды
IF $interaction_type = 'rental_demo':
 npc = 1
 $npc_id = 'realtor'
 $return_lock  = 'shop_general'   
 $special_action[3] = {
 $place = 'demo'
 $place_name['demo'] = 'Смотр помещений'
 $place_info['demo'] = '   Надо хорошенько осмотреть все помещения, чтобы мне не подсунули какую-нибудь фигню.' 
 $bg['demo'] = $interior_study['<<$demo_var>>']
} 
 $special_action[5] = {npc = 0}
 $special_action[6] = {
npc = 1 
$bg['demo'] = $interior_bed['<<$demo_var>>']
} 
 $special_action[7] = {npc = 0}
 $special_action[8] = {
npc = 1
$bg['demo'] = $interior_bath['<<$demo_var>>']
} 
 $special_action[9] = {npc = 0}
 $special_action[10] = {
npc = 1
$bg['demo'] = $interior_kitchen['<<$demo_var>>']
} 
 $special_action[11] = {npc = 0}
 $special_action[12] = {
npc = 1
$bg['demo'] = $interior_study['<<$demo_var>>']
} 
 $special_action[13] = {
npc = 0 
$bg['demo'] = $interior_bouduar['<<$demo_var>>']
} 
 $special_action[14] = {
$bg['demo'] = $interior_dungeon['<<$demo_var>>']
} 
 $special_action[15] = {
$bg['demo'] = $interior_lab['<<$demo_var>>']
} 
 $special_action[16] = {
npc = 1
$bg['demo'] = $interior_study['<<$demo_var>>']
gt 'choice_screen'
} 
 $no = {
$place = 'realtor_office'
gt 'shop_general'
 }
 txt_count = 16
END

!Тип взаимодействия - подписание аренды
IF $interaction_type = 'rental_sex':
 npc = 1
 $npc_id = 'realtor'
 menu_form = 0
 gs 'new_estate'
 $place = 'bedroom'
 $return_lock  = 'menu_form'  
 $special_action[3] = {npc = 0} 
 IF master_excitement > 0:
  txt_count = 6
  $special_image[3] = 'girls\sex\bj_general.gif'
  $special_image[4] = 'girls\sex\bj_general.gif'
  $special_image[5] = 'girls\sex\anal_general.gif'
  $special_image[6] = 'girls\sex\anal_general.gif'
  $play_sound[3] = '<<$bj_sound[2]>>'
  $play_sound[5] = '<<$assfuck_sound[2]>>'  
  ejaculation_intensity = master_excitement
  dynamic $master_ejaculate 
  dynamic $master_energy_drop 
  master_hygiene_rate += 10
  house_mess_rate += 10
  copyarr '$text', '$realtor_bonus_sex'
  else
  dynamic $master_energy_drop 
  txt_count = 4
  cooked_food = 5
  house_mess_rate = 0
  house_mess = 0
  sparks += 5
  copyarr '$text', '$realtor_no_sex'
 END 
END

!Тип взаимодействия - бордель
IF $interaction_type = 'whore':
 $place = 'brothel'
 $return_lock  = 'shop_general'  
 txt_count = 3
 sparks -= whore_price
 ejaculation_intensity = whore_quality 
 dynamic $master_ejaculate 
 dynamic $master_energy_drop 
 add_master_hygiene = 5 * whore_quality
 master_hygiene_rate += add_master_hygiene
 $picture_type = 'pic_jpg'
 dynamic $special_part  
END

!Тип взаимодействия - деньги в долг
IF $interaction_type = 'moneylender':
 $return_lock  = 'shop_general'  
 $place = 'moneylander' 
 $npc_id = 'moneylender'
 npc = 1
 txt_count = 1
 $text[1] = "  [РОСТОВЩИК]
   - Вот твои искры, работорговец. Если вернешь долг вовремя, возможно я увеличу объем твоего кредита. Возможно. Но если ты с этим не справишься - то я укорочу твою жизнь. И вот это - точно. Жду тебя через <<debt_count>> дней. Не опаздывай."
 sparks += loan 
END

!Тип взаимодействия - покупка предмета для текущей рабыни
IF $interaction_type = 'buy_item':
 txt_count = 1
 $return_lock  = 'shop_general'  
 $picture_type = 'pic_jpg'
 $special_bg  = "<<$item_demo[$item_id]>>"  
 $text[1] = $item_description[$item_id]
 IF sparks => item_price[$item_id]: 
  $do_buy = {
  sparks -= item_price[$item_id]
  slave_inventory[$item_id] += 1} 
  gt 'item_buy' 
  else
  $text[1] = '   Цена товара составляет <<item_price[$item_id]>> искр, что несколько больше чем вы можете позволить себе в данный момент, однако это дело поправимое. Надо просто продать пару рабынь...'
 END
END

!Тип взаимодействия - покупка предмета на склад
IF $interaction_type = 'buy_item_home':
 txt_count = 1
 $return_lock  = 'shop_general'  
 $picture_type = 'pic_jpg'
 $special_bg  = "<<$item_demo[$item_id]>>"  
 $text[1] = $item_description[$item_id]
 IF sparks => item_price[$item_id]: 
  $do_buy = {
  sparks -= item_price[$item_id]
  inventory[$item_id] += 1} 
  gt 'item_buy' 
  else
  $text[1] = '   Цена товара составляет <<item_price[$item_id]>> искр, что несколько больше чем вы можете позволить себе в данный момент, однако это дело поправимое. Надо просто продать пару рабынь...'
 END
END

IF $interaction_type = 'buy_item_personal':
 if sound_on = 1: play 'content\snd\kkm.mp3' 
 $return_lock  = 'shop_general'  
 sparks -= item_price[$item_id]
 dynamic '<<$item_id>> += multiple' 
 gt 'shop_general'
END

!Тип взаимодействия - услуги
IF $interaction_type = 'service':
 txt_count = 2
 $return_lock  = 'shop_general'  
 $picture_type = 'pic_jpg'
 $special_bg  = $service_picture   
 IF sparks => item_price[$item_id]: 
  sparks -= service_price
  dynamic $do_service
  else
  txt_count = 1  
  $special_bg  = ''     
  $text[1] = '   Цена услуги составляет <<service_price>> искры, что несколько больше чем вы можете позволить себе в данный момент, однако это дело поправимое. Пора продавать рабыню...'
 END
END

!Тип взаимодействия - угостить девушку в баре
IF $interaction_type = 'hit_on_patron':
 $place = 'tabern'
 IF already_done_today[interaction_city] <= 0:
  a = RAND (1,2)
  if a = 1: $npc_id = 'брина_рыжая' 
  if a = 2: $npc_id = 'poison' 
  npc = 1
  dynamic $npc_flirt[$npc_id]
  npc_reveal[$npc_id] = 1
  sparks -= 2
  else
  txt_count = 1
  $text[1] = '   Вы осматриваете заведение в поиске других скучающих и одиноких женщин, но ни одной подходящей кандидатуры для ухаживаний не видно. Наверное, стоит попытать счастья завтра.'
 END  
END

!Метки исполнения
already_done_today[interaction_city] += 1 
already_done_ever[interaction_city] += 1 

txt = 1
GT 'interaction_screen_city'
--- interaction_result_city ---------------------------------

